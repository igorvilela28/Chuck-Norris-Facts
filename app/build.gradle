apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "$rootDir/tools/jacoco.gradle"

android {

    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]

    defaultConfig {
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        applicationId globalConfiguration["androidApplicationId"]
        versionCode globalConfiguration["androidVersionCode"]
        versionName globalConfiguration["androidVersionName"]
        testInstrumentationRunner "com.igorvd.chuckfacts.testutils.runner.AppRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug //must be replaced, just for testing
        }

        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            multiDexEnabled true
            testCoverageEnabled true
        }
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    testOptions {
        animationsDisabled true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':domain')
    implementation project(':data')

    def appDependencies = rootProject.ext.appDependencies
    def appTestDependencies = rootProject.ext.appTestDependencies
    def appAndroidTestDependencies = rootProject.ext.appAndroidTestDependencies
    def appKapt = rootProject.ext.appKapt
    def appAndroidTestKapt = rootProject.ext.appAndroidKapt

    appDependencies.each { v -> implementation v }
    appKapt.each { v -> kapt v }

    //unit test
    appTestDependencies.each { v -> testImplementation v }

    //integration tests
    appAndroidTestDependencies.each { v -> androidTestImplementation v }
    appAndroidTestKapt.each { v -> kaptAndroidTest v }
    androidTestImplementation('androidx.test:runner:1.1.1') {
        exclude group: 'androidx.legacy', module: 'legacy-support-v4'
        exclude group: 'com.google.android.material', module: 'material'
    }
    androidTestUtil "androidx.test:orchestrator:1.1.1"

}

androidExtensions {
    experimental = true
}

kapt {
    mapDiagnosticLocations = true
}
